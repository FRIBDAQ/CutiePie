#
#   Tests for the spectcl::wsCreate command
#
package require tcltest
load    .libs/libSpecTclExperiment.so

namespace import ::tcltest::*


proc setup {} {
    global exphandle
    global runHandle 

    file delete ./expdb.db;	# Destroy anything that might be hanging on.
    file delete ./run1.db
    
    set exphandle [::spectcl::expcreate ./expdb.db]
    ::spectcl::run create $exphandle 1 "This is a test"
    set runHandle [::spectcl::evtcreate $exphandle 1 ./run1.db]
  


}

proc cleanup {} {
    global exphandle
    global runHandle

    ::spectcl::expclose $exphandle
    ::spectcl::evtclose $runHandle
    
    file delete ./expdb.db
    file delete ./run1.db
    
}

#----------------------- The tests --------------------------


test defined {wsCreate command is defined}  \
    -body {
	info commands ::spectcl::wsCreate
    }  -result "::spectcl::wsCreate"

test fail_1 {wsCreate command requires an experiment handle} \
    -body {
	set status [catch {::spectcl::wsCreate} msg]
	set result [list $status $msg]
    } -result [list 1 "Incorrect number of command line parameters"]


test fail_2 {wsCreate command requires a known handle} \
    -body {
	set status [catch {::spectcl::wsCreate junkyhandle} msg]
	set result [list $status $msg]
    } -result [list 1 "spectcl::wsCreate - invalid database handle junkyhandle"]

test fail_3 {wsCreate command's handle must be an exphandle not e.g. an event handle} \
    -setup {
	setup
    } \
    -cleanup {
	cleanup
	
    } \
    -body {
	set status [catch [list ::spectcl::wsCreate $runHandle] msg]
	set result [list $status $msg]
    } -result [list 1   "NOT_EXPDATABASE - Database is not an experiment database"] 


test fail_4 {wsCreate fails with ok handle but no path.} \
    -setup {
	setup
    } \
    -cleanup {
	cleanup
    }  \
    -body {
	set status [catch [list ::spectcl::wsCreate $exphandle]  msg]
	list $status $msg
    } -result [list 1  "Incorrect number of command line parameters"]

test ok {wsCreat should create a database if everything is good} \
    -setup {
	setup
	file delete ./ws.db
    } \
    -cleanup {
	cleanup
	file delete ./ws.db
    } \
    -body {
	::spectcl::wsCreate $exphandle ./ws.db
	set status [file exists ./ws.db]
    } -result 1

#  Report the test results.

cleanupTests