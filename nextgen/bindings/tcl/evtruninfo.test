#
#
# Test for spectcl::evtruninfo
#

package require tcltest
load .libs/libSpecTclExperiment.so

namespace import ::tcltest::*

proc setup {} {
    set tmpdir  [makeDirectory databases]
    set expfile [file join $tmpdir exp.db]
    set evtfile [file join $tmpdir evt.db]

    set exphandle [::spectcl::expcreate $expfile]
    ::spectcl::run create $exphandle 1 {This is a test title}
    ::spectcl::evtcreate $exphandle 1 $evtfile


    return [list $exphandle $expfile $evtfile]

}

proc cleanup {exphandle expfile evtfile} {
    ::spectcl::expclose $exphandle

    catch {file delete $expfile}
    catch {file delete $evtfile}

}

#-------------------- The tests.

test evtruninfo-cmdexists {Command must exist} \
    -body {
	info commands ::spectcl::evtruninfo
    } -result ::spectcl::evtruninfo

test evtruninfo-argcount {Command must have a parameter} \
    -body {
	catch {::spectcl::evtruninfo}
    } -result 1

test evtruninfo-mbhandle {Command parameter must be a handle of the right sort} \
    -setup {
	set info [setup]
	set i 0
	foreach varname {exphandle expfile evtfile} item $info {
	    set $varname $item
	    incr i
	} 
    } \
    -cleanup {
	cleanup $exphandle $expfile $evtfile
    } \
    -body {
	set evhandle [::spectcl::evtopen  $evtfile]
	set status [catch {::spectcl::evtruninfo $evhandle}]
	::spectcl::evtclose $evhandle
	set status
    } -result 1

test evtruninfo-ok-default {Get the right info when attached in default spot} \
    -setup {
	set info [setup]
	set i 0
	foreach varname {exphandle expfile evtfile} item $info {
	    set $varname $item
	    incr i
	} 
    } \
    -cleanup {
	cleanup $exphandle $expfile $evtfile
    } \
    -body {
	::spectcl::attach $exphandle $evtfile
	set info [::spectcl::evtruninfo $exphandle]
	::spectcl::detach $exphandle
	set info [lrange $info 0 1]
    } -result {1 {This is a test title}}

test evtruninfo-ok-otherpoint {Get right info when attached to a nondefault spot} \
    -setup {
	set info [setup]
	set i 0
	foreach varname {exphandle expfile evtfile} item $info {
	    set $varname $item
	    incr i
	} 
    } \
    -cleanup {
	cleanup $exphandle $expfile $evtfile
    } \
    -body {
	::spectcl::attach $exphandle $evtfile otherpoint
	set info [::spectcl::evtruninfo $exphandle otherpoint]
	::spectcl::detach $exphandle otherpoint
	set info [lrange $info 0 1]
    } -result {1 {This is a test title}}
#--------------------- reporting results.

cleanupTest