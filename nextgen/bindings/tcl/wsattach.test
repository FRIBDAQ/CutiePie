#
#   Tests for the spectcl::wsAttach command
#
package require tcltest
package require sqlite3

load    .libs/libSpecTclExperiment.so

namespace import ::tcltest::*

#--- Fixture code:



proc setup {} {
    global exphandle
    global runHandle 

    file delete ./expdb.db;	# Destroy anything that might be hanging on.
    file delete ./run1.db
    
    set exphandle [::spectcl::expcreate ./expdb.db]
    ::spectcl::run create $exphandle 1 "This is a test"
    set runHandle [::spectcl::evtcreate $exphandle 1 ./run1.db]
  


}

proc cleanup {} {
    global exphandle
    global runHandle

    ::spectcl::expclose $exphandle
    ::spectcl::evtclose $runHandle
    
    file delete ./expdb.db
    file delete ./run1.db
    
}
#--------------------------------- The tests ----------------


test defined {wsAttach command is defined} \
    -body {
	info commands ::spectcl::wsAttach
    } -result ::spectcl::wsAttach


test fail_1 {wsAttach command requires a database handle} \
    -body {
	set status [catch ::spectcl::wsAttach msg]
	list $status $msg
    } -result [list 1 "Incorrect number of command line parameters"]

test fail_2 {wsAttach command requires that the database handle be known} \
    -body {
	set status [catch [list ::spectcl::wsAttach no-such-handle] msg]
	list $status $msg
    } -result [list 1 "spectcl::wsAttach - invalid database handle no-such-handle"]

test fail_3 {wsAttach database handle must be for an experiment} \
    -setup {
	setup
    } \
    -cleanup {
	cleanup 
    } \
    -body {
	set status [catch [list ::spectcl::wsAttach $runHandle] msg]
	list $status $msg
    } -result [list 1 "NOT_EXPDATABASE - Database is not an experiment database"]

test fail_3 {wsAttach needs a database filename} \
    -setup {
	setup
    } \
    -cleanup {
	cleanup
    } \
    -body {
	set result [catch [list ::spectcl::wsAttach $exphandle] msg]
	list $result $msg
	
    } -result [list 1  "Incorrect number of command line parameters"]

test fail_4 {wsAttach's database path must exist.} \
    -setup {
	setup
	file delete ./ws.db
    } \
    -cleanup {
	cleanup
    } \
    -body {
	set result [catch [list ::spectcl::wsAttach $exphandle ./ws.db] msg]
	list $result $msg
    } -result [list 1 "OPEN_FAILED - Unable to open database"]


test ok_1 {wsAttach with database path but no attach point} \
    -setup {
	file delete ./ws.db
	setup
	::spectcl::wsCreate $exphandle ./ws.db
    } \
    -cleanup {
	cleanup
	file delete ./ws.db
    } \
    -body {
	::spectcl::wsAttach $exphandle ./ws.db

    } -result [list]

test ok_t {wasAttach with database path and attach point} \
    -setup {
	file delete ./ws.db
	setup
	::spectcl::wsCreate $exphandle ./ws.db
    } \
    -cleanup {
	cleanup
	file delete ./ws.db
    } \
    -body {
	::spectcl::wsAttach $exphandle ./ws.db MYPOINT

    } -result [list]

test fail_5 {wsAttach .. too many parameters} \
    -setup {
	file delete ./ws.db
	setup
	::spectcl::wsCreate $exphandle ./ws.db
	
    } \
    -cleanup {
	cleanup
	file delete ./ws.db
    } \
    -body {
	set result [catch [list ::spectcl::wsAttach $exphandle ./ws.db extra parameters] msg]
	list $result $msg
    } -result [list 1 "::spectcl::wsAttach - too many command parameters"]

cleanupTests