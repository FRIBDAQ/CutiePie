#
#  Tests for the spectcl::evtclose command.
#
package require tcltest
load    .libs/libSpecTclExperiment.so

namespace import ::tcltest::*

#
# much of the setup/cleanup is standard...
# Create an experiment database, and put run 1 in it.
#
proc setup {} {
    set tmpdir  [makeDirectory databases]
    set expfile [file join $tmpdir exp.db]
    set evtfile [file join $tmpdir evt.db]

    set exphandle [::spectcl::expcreate $expfile]
    ::spectcl::run create $exphandle 1 {This is a test title}
    set evhandle [::spectcl::evtcreate $exphandle 1 $evtfile]

    return [list $exphandle $expfile $evtfile $evhandle]

}

proc cleanup {exphandle expfile evtfile} {
    ::spectcl::expclose $exphandle

    catch {file delete $expfile}
    catch {file delete $evtfile}

}

#----------------------- tests begin here -------------------

test fail_1 {Close without handle can't work} \
    -body {
	catch {::spectcl::evtclose}
    } -result 1

test fail_2 {Close of something that is not a database handle should also fail} \
     -body {
	 catch {::spectcl::evtclose junk}
     } -result 1
test fail_3 {Close of something that is not an event handle should also fail} \
    -setup {
	set setupdata [setup]
	set exphandle [lindex $setupdata 0]
	set expfile   [lindex $setupdata 1]
	set evtfile   [lindex $setupdata 2]
	set evhandle  [lindex $setupdata 3]
    } \
    -cleanup {
	cleanup $exphandle $expfile $evtfile
    } \
    -body {
	catch {::spectcl::evtclose $exphandle}
    } -result 1

test evtcloseOk {Close of an event file should work} \
    -setup {
	set setupdata [setup]
	set exphandle [lindex $setupdata 0]
	set expfile   [lindex $setupdata 1]
	set evtfile   [lindex $setupdata 2]
	set evhandle  [lindex $setupdata 3]
    } \
    -cleanup {
	cleanup $exphandle $expfile $evtfile
    } \
    -body {
	catch {::spectcl::evtclose $evhandle}
    } -result 0
